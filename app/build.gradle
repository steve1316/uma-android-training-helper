plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.steve1316.uma_android_training_helper"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 24
        versionName "2.3.0"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                // Allow layout XMLs to get a reference to the application's version number.
                variant.resValue "string", "versionName", "v" + variant.versionName

                // Auto-generate the file name.
                variant.outputs.all { output ->
                    def versionName = defaultConfig.versionName
                    def architecture = output.filters[0].identifier
                    outputFileName = "v${versionName}-UmaAndroidTrainingHelper-${architecture}.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Specify which architecture to make apks for, or set universalApk to true for an all-in-one apk with increased file size.
    splits {
        abi {
            enable true
            reset()
            include "armeabi","armeabi-v7a",'arm64-v8a',"mips","x86","x86_64"
            // include "arm64-v8a"
            universalApk false
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    // OpenCV Android 4.5.1 for image processing.
    implementation project(path: ':opencv')

    // Tesseract4Android for OCR text recognition.
    implementation 'cz.adaptech.android:tesseract4android:2.1.1'

    // string-similarity to compare the string from OCR to the strings in data.
    implementation 'net.ricecode:string-similarity:1.0.0'

    // Klaxon to parse JSON data files.
    implementation 'com.beust:klaxon:5.5'

    // AppUpdater for notifying users when there is a new update available.
    implementation 'com.github.javiersantos:AppUpdater:2.7'
}